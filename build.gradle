apply plugin: 'java'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    aptCompile
}

configurations {
    all*.exclude group: 'xml-apis'
    all*.exclude group: 'junit'
    all*.exclude module: 'slf4j-log4j12'
}

def groupVersionOverrides = [
        "com.google.guava"   : "$guavaVersion",
        "org.springframework": "$springVersion",
        "org.hibernate"      : "$hibernateVersion",
        "com.mysema.querydsl": "$querydslVersion",
        "org.slf4j"          : "$slf4jVersion"
]

configurations.all {

    resolutionStrategy {

        failOnVersionConflict()

        eachDependency { DependencyResolveDetails details ->

            def overrideVersion = groupVersionOverrides[details.requested.group]

            if (overrideVersion != null && details.requested.version != overrideVersion) {
                details.useVersion overrideVersion
            }
        }
    }
}

dependencies {

    aptCompile(
            "com.mysema.querydsl:querydsl-apt:$querydslVersion")

    compile(
            "ch.qos.logback:logback-classic:1.0.13",
            "com.mysema.querydsl:querydsl-jpa:$querydslVersion",
            "com.mysema.querydsl:querydsl-collections:$querydslVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework.data:spring-data-jpa:$springdataVersion",
            "org.hsqldb:hsqldb:2.2.8",
            "joda-time:joda-time:2.1",
            "org.jadira.usertype:usertype.core:3.1.0.GA",
            "org.hibernate:hibernate-ehcache:$hibernateVersion")

    testCompile(
            "org.springframework:spring-test:$springVersion",
            "org.testng:testng:6.3",
            "org.hamcrest:hamcrest-library:1.3",
            "org.hamcrest:hamcrest-integration:1.3")
}

sourceSets {

// source set reference object for generated sources
    apt {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task queryDsl(type: JavaCompile, group: 'build', description: 'Invoke annotation processor with main classpath + apt classpath') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.aptCompile
    options.compilerArgs = [
            '-proc:only',
            '-processor', 'com.mysema.query.apt.jpa.JPAAnnotationProcessor'
    ]
    destinationDir = sourceSets.apt.java.srcDirs.iterator().next() // destinationDir requires single file object
}

compileJava {
    dependsOn queryDsl
    source = sourceSets.main.java + sourceSets.apt.java
// combine the generated source with main, could use sourceSets.all
}

clean {
    delete sourceSets.apt.java.srcDirs
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}