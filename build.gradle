apply plugin: 'java'

targetCompatibility = "1.7"
sourceCompatibility = "1.7"

repositories {
    mavenCentral()
}

configurations {
    aptCompile
}

dependencies {

    aptCompile group: 'com.mysema.querydsl', name: 'querydsl-apt', version: '3.2.4'

    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'

    compile group: 'com.google.guava', name: 'guava', version: '13.0.1'

    compile group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: '3.2.4'

    compile group: 'com.mysema.querydsl', name: 'querydsl-collections', version: '3.2.4'

    compile group: 'org.springframework', name: 'spring-context', version: '3.2.4.RELEASE'
    compile group: 'org.springframework', name: 'spring-orm', version: '3.2.4.RELEASE'

    compile('org.springframework.data:spring-data-jpa:1.4.2.RELEASE') {
        exclude(group: 'org.junit')
        exclude(group: 'org.hamcrest')
    }

    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '4.2.7.Final'

    compile group: 'org.hibernate', name: 'hibernate-ehcache', version: '4.2.7.Final'

    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.2.8'

    compile group: 'javax.inject', name: 'javax.inject', version: '1'

    compile group: 'joda-time', name: 'joda-time', version: '2.1'
    compile group: 'org.jadira.usertype', name: 'usertype.core', version: '3.0.0.GA'

    testCompile group: 'org.testng', name: 'testng', version: '6.3'
    testCompile group: 'org.springframework', name: 'spring-test', version: '3.2.4.RELEASE'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-integration', version: '1.3'

}

sourceSets {

    // source set reference object for generated sources
    apt {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task queryDsl(type: JavaCompile, group: 'build', description: 'Invoke annotation processor with main classpath + apt classpath') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.aptCompile
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.apt.java.srcDirs.iterator().next() // destinationDir requires single file object
}

compileJava {
    dependsOn queryDsl
    source = sourceSets.main.java + sourceSets.apt.java // combine the generated source with main, could use sourceSets.all
}

clean {
    delete sourceSets.apt.java.srcDirs
}
